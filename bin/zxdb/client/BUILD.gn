# Copyright 2018 The Fuchsia Authors. All rights reserved.
# Use of this source code is governed by a BSD-style license that can be
# found in the LICENSE file.

static_library("client") {
  friend = [ ":tests" ]

  public = [
    "arch_info.h",
    "breakpoint.h",
    "breakpoint_observer.h",
    "breakpoint_settings.h",
    "disassembler.h",
    "frame.h",
    "memory_dump.h",
    "process.h",
    "process_observer.h",
    "session.h",
    "system.h",
    "system_observer.h",
    "target.h",
    "target_observer.h",
    "thread.h",
    "thread_observer.h",
  ]

  sources = [
    "arch_info.cc",
    "breakpoint.cc",
    "breakpoint_impl.cc",
    "breakpoint_impl.h",
    "disassembler.cc",
    "frame.cc",
    "frame_impl.cc",
    "frame_impl.h",
    "memory_dump.cc",
    "process.cc",
    "process_impl.cc",
    "process_impl.h",
    "session.cc",
    "system.cc",
    "system_impl.cc",
    "system_impl.h",
    "target.cc",
    "target_impl.cc",
    "target_impl.h",
    "thread.cc",
    "thread_impl.cc",
    "thread_impl.h",
  ]

  public_deps = [
    ":common",
    "symbols",
    "//garnet/lib/debug_ipc:client",
    "//garnet/lib/debug_ipc/helper",
    "//garnet/public/lib/fxl",
    "//third_party/llvm/lib/Target/AArch64/Disassembler",
    "//third_party/llvm/lib/Target/X86/Disassembler",
  ]

  deps = [
    "//third_party/llvm/lib/MC",
    "//third_party/llvm/lib/Object",
  ]
}

# Shared low-level stuff between "client" and "symbols".
static_library("common") {
  public = [
    "client_object.h",
    "err.h",
    "host_utils.h",
  ]
  sources = [
    "client_object.cc",
    "err.cc",
    "host_utils.cc",
  ]
}

source_set("tests") {
  testonly = true
  sources = [
    "disassembler_unittest.cc",
    "memory_dump_unittest.cc",
  ]

  deps = [
    ":client",
    "symbols:tests",
    "//third_party/googletest:gtest",
  ]
}
