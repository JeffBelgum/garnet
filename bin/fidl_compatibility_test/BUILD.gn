# Copyright 2018 The Fuchsia Authors. All rights reserved.
# Use of this source code is governed by a BSD-style license that can be
# found in the LICENSE file.

import("//build/fidl/fidl.gni")
import("//build/package.gni")

fidl("compatibility_test_service") {
  sources = [
    "compatibility_test_service.fidl",
  ]
}

source_set("echo_client_app") {
  sources = [
    "echo_client_app.cc",
    "echo_client_app.h"
  ]
  deps = [
    ":compatibility_test_service",
    "//garnet/public/lib/app/cpp",
    "//garnet/public/lib/svc/cpp",
    "//zircon/public/lib/zx",
  ]
}

executable("fidl_compatibility_test") {

  testonly = true

  sources = [
    "compatibility_test.cc",
  ]

  deps = [
    ":compatibility_test_service",
    ":echo_client_app",
    "//garnet/public/lib/app/cpp",
    "//garnet/public/lib/fidl/cpp",
    "//third_party/googletest:gtest",
    "//zircon/public/lib/async-default",
    "//zircon/public/lib/async-loop-cpp",
  ]
}

executable("compatibility_test_server_cpp_bin") {
  output_name = "compatibility_test_server_cpp"

  sources = [
    "compatibility_test_server_cpp.cc"
  ]

  deps = [
    ":compatibility_test_service",
    ":echo_client_app",
    "//garnet/public/lib/app/cpp",
    "//garnet/public/lib/fidl/cpp",
    "//zircon/public/lib/async-default",
    "//zircon/public/lib/async-loop-cpp",
  ]
}

package("package") {
  package_name = "fidl_compatibility_test"

  deps = [
    ":fidl_compatibility_test",
  ]

  testonly = true

  tests = [
    {name = "fidl_compatibility_test"},
  ]
}

package("fidl_compatibility_test_server_cpp") {
  deps = [
    ":compatibility_test_server_cpp_bin",
  ]

  testonly = true

  binary = "compatibility_test_server_cpp"
}
